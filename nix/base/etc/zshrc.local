HISTFILE=~/.zsh_history
HISTSIZE=100000
# Zsh:
# 1. Stores this in a signed 64bit int
# 2. Will modify it with x:(x+x/5) before performing a comparison to decide if it needs to rewrite the history file.
# Thefore, values like 2**63-1 will trigger a rewrite on every command, which a) is slow and b) appears to be racy and carry a risk of data loss in the case of parallel access by several shells.
# We for now set this to 2**62, which is small enough to not trigger the comparison bug and hopefully large enough that we will nevertheless not run out before we can increase the size of the relevant variables.
SAVEHIST=4611686018427387904

# NixOS annoyingly insists on defining a command_not_found_handler() that breaks stuff. Get rid of it here.
unfunction command_not_found_handler || true

export PATH=~/.exe:${PATH}

if [[ -f /etc/NIXOS ]]; then
  SHELL=/run/current-system/sw/bin/zsh
else
  SHELL=/usr/bin/zsh
fi

setopt incappendhistory notify extended_history hist_ignore_space bsdecho hist_fcntl_lock
unsetopt beep extendedglob nomatch share_history hist_ignore_dups histfcntllock
bindkey -e

autoload -Uz compinit
compinit
# End of lines added by compinstall
### Remove autocompletion methods that get in our way.
compdef -d git # messes up -add path completion.
compdef -d umount	# breaks normal completion
compdef -d fusermount	# same
compdef -d mdadm	# yup.

### prompt setup
if [[ ${EUID} == 0 ]] ; then
   PS1=$'%{\033[90m%}%D{%d.%H:%M:%S} %{\033[01;31m%}%n@%m%{\033[01;34m%} %~ %# %{\033[00m%}'
else
   PS1=$'%{\033[90m%}%D{%d.%H:%M:%S} %{\033[01;32m%}%n@%m%{\033[01;34m%} %~ %# %{\033[00m%}'
fi

for v in RPROMPT RPS1 RPROMPT2 RPS2; { unset $v }
#RPS1=$'%D{%d.%H:%M:%S}'

### Navigation keys
## These are ctrl+{right,left}arrow
bindkey ';5D' backward-word
bindkey ';5C' forward-word

### Env vars
# EDITOR
export VISUAL='emacs -nw'
# For tree(1)
eval $(dircolors -b /etc/DIR_COLORS)
# Less
export LESS='-RQP%f %bB/%B (%lB/%L) ?e(EOF):[%pB\%].?x - Next\: %x.:?pB%pB\%..%t'
# iostat
export S_COLORS=always

### Shell style settings
# Colored completions. :)
# Keep this after dircolors above, so we can use the env var.
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

### Helpers
# Autojump
export AUTOJUMP_KEEP_ALL_ENTRIES=1
export AUTOJUMP_KEEP_SYMLINKS=1
test -f /usr/share/autojump/autojump.zsh && . /usr/share/autojump/autojump.zsh

# Temporary hacks
export MPV_HOME=/etc/mpv
